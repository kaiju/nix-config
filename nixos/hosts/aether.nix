{ pkgs, ... }:
{
  imports = [
    ../modules/bluetooth.nix
    ../modules/xorg.nix
    ../modules/sdr.nix
    ../modules/containers.nix
    ../modules/audio.nix
  ];

  home-manager.users.josh.imports = [
    ../../hm/obsidian.nix
    ../../hm/comms.nix
    ../../hm/sublime-text.nix
    ../../hm/visual-studio-code.nix
    ../../hm/gui-environment.nix
    ../../hm/gnupg.nix
    ../../hm/rbw.nix
    ../../hm/xorg.nix

    ../../home-manager/users/josh/workstation.nix
    ../../home-manager/development.nix
    ../../home-manager/wayland.nix
    ../../home-manager/zed-editor.nix
  ];

  boot.kernelPackages = pkgs.linuxPackages_zen;

  mast.wallpaper = "${pkgs.mastpkgs.wallpaper}/lenovo-1.jpg";

  programs.udevil.enable = true;

  services.gvfs.enable = true;

  services.greetd = {
    enable = true;
    settings.default_session = {
      command = "${pkgs.cage}/bin/cage -s -d -- ${pkgs.foot}/bin/foot -f monospace:size=25 ${pkgs.tuigreet}/bin/tuigreet -s ${pkgs.sway}/share/wayland-sessions -t -r";
    };
  };

  # hmm xdg tweaking
  environment.pathsToLink = [
    "/share/xdg-desktop-portal"
    "/share/applications"
  ];

  programs.xwayland.enable = true;

  # is this needed??
  services.gnome.gnome-keyring.enable = true;
  security.pam.services.login.enableGnomeKeyring = true;

  # laptop specific i3 status configuration
  home-manager.users.josh.programs.i3status-rust = {
    bars.default.blocks = [
      {
        block = "backlight";
        device = "intel_backlight";
      }
      {
        block = "battery";
        device = "BAT0";
      }
      {
        block = "sound";
        format = " $icon $output_description{ $volume|} ";
      }
      {
        block = "net";
        device = "wlp0s20f3";
        format = " $icon $ssid $ip ";
      }
    ];
  };

  # High DPI settings
  services.xserver = {
    dpi = 210;
    # Set a custom scaled resolution for our display. This seems to solve a bunch of screen tearing issues compared to
    # just running xrandr --output --scale on startup.
    # This modeline was generated by:
    # - Running xrandr --output eDP-1 --scale 1.3x1.3
    # - xrandr --current | grep current to get resolution
    # - cvt <resolution> to generate the appropriate modeline
    extraConfig = ''
      Section "Monitor"
        Identifier "eDP-1"
        # 3328x1872 59.99 Hz (CVT 6.23M9) hsync: 116.31 kHz; pclk: 532.25 MHz
        Modeline "3328x1872_60.00"  532.25  3328 3592 3952 4576  1872 1875 1880 1939 -hsync +vsync
        Option "PreferredMode" "3328x1872_60.00"
      EndSection
    '';
  };

  home-manager.users.josh.wayland.windowManager.hyprland.settings = {
    monitor = "eDP-1, 2560x1440@60, 0x0, 1.6";
  };

  home-manager.users.josh.wayland.windowManager.sway.config.output = {
    eDP-1 = {
      scale = "1.6";
    };
  };

  home-manager.users.josh.xsession.profileExtra = ''
    export GDK_SCALE=2
    export GDK_DPI_SCALE=0.5
    export QT_AUTO_SCREEN_SET_FACTOR=0
    export QT_SCALE_FACTOR=2
    export QT_FONT_DPI=96
  '';

  home-manager.users.josh.xresources.properties = {
    "Xft.dpi" = "210";
    "*.dpi" = "210";
    "*dpi" = "210";
  };

  home-manager.users.josh.home.pointerCursor = {
    package = pkgs.vanilla-dmz;
    name = "Vanilla-DMZ";
    size = 64;
    x11.enable = true;
  };

  home-manager.users.josh.programs.alacritty.settings.env = {
    "WINIT_X11_SCALE_FACTOR" = "2.3";
  };

  # end

  networking = {
    useDHCP = false;
    networkmanager.enable = true;
    interfaces = {
      enp0s31f6.useDHCP = true;
      wlp0s20f3.useDHCP = true;
    };
  };

  services = {
    fwupd.enable = true; # firmware updater
    fprintd.enable = true; # fingerprint reader
    printing.enable = true;
  };

  environment.systemPackages = with pkgs; [
    _86Box
    usbutils
    xorg.xdpyinfo
    qt5.qtwayland # :(

    (retroarch.withCores (
      cores: with cores; [
        bsnes
        mesen
        beetle-lynx
      ]
    ))

    scummvm
  ];

  programs.steam = {
    enable = true;
  };

}
